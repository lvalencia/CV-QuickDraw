# start with a launch state
launch
  if HTML.isHTMLPresent()
    directive createHtmlStartDirective("index.html")
    say "Welcome to Quick Draw! A game of skill and reflexes. "
    -> gameRules
  else
    say "Sorry, your device does not support the latest HTML version 0.2. "
    -> goodbye

gameRules
  say "The game is simple, you're a wandering traveler, gather your weapons and face off against your foes in a test of speed. "
  say "You'll see a flash and know which weapon to raise against your opponent, raise it fast and hold it steady. "
  say "Too late and you'll be cut down, too early and your foe will see through you. "
  say "Ready? Let's do this! "
  -> getTools

goodbye
  say "Thank you traveler, that's good for now."
  END

getTools
  say "In order to play we're going to have to gather your ninja tools, scout around and find {promptWeapons()}. "
  say "When you're ready say Alexa, I'm ready. "
  when ReadyIntent
    or "ready"
    or "I'm ready"
    say "Your foe approaches..."
    directive sendStartGameMessage()
    -> gameLoop

gameLoop
  when Alexa.Presentation.HTML.Message
    directive createHtmlHandleMessageDirectiveResponse($request)
    LISTEN events

  when AMAZON.HelpIntent
    say "The game is simple. "
    say "You'll see a flash and know which weapon to raise against your opponent, raise it fast and hold it steady. "
    LISTEN events

  when AMAZON.StopIntent
    -> goodbye
